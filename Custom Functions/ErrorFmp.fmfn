/**
 * =====================================
 * ErrorFmp ( theErrorCode ; theErrorInfo )
 *
 * RETURNS:
 *		Let Notation containing information about a FileMaker error
 *		and the environment it occurred in.
 *
 * PARAMETERS:
 *		theErrorCode = (number) code for a FileMaker error
 *		theErrorInfo = (text, optional) some text describing the particular
 *			instance of this error. This should help a developer locate where
 *			in a script the error occured.
 *			(e.g., "create new invoice record")
 *
 * DEPENDENCIES:
 *		Custom Functions: Error, ErrorDescriptionFmp
 *
 * HISTORY:
 *		MODIFIED on 2019-JUN-01 by Daniel Smith dan@filemaker.consulting
 *			- move error code to description mapping code to another function
 *		MODIFIED on 2018-JUN-01 by Daniel Smith dan@filemaker.consulting
 *			- convert to JSON result
 *			- change permissions related error test to include 2xx errors
 *			- add CurrentPrivilegeSetName on permission error
 *			- add ApplicationVersion and HostApplicationVersion when command is unavailable
 *		MODIFIED on 2013-NOV-22 by Daniel Smith dansmith65@gmail.com
 *			- rename to ErrorFmp, update documentation
 *		CREATED on 2013-OCT-11 Daniel Smith dansmith65@gmail.com
 *
 * REFERENCES:
 *		http://www.filemaker.com/help/html/error_codes.html
 *		https://github.com/dansmith65/FileMaker-Error-Handling
 * =====================================
 */
 
Let ( [
	~error = Error ( "Fmp" ; theErrorCode ; ErrorDescriptionFmp ( theErrorCode ) ; theErrorInfo )
] ;
	Case (
		theErrorCode = 0 ;
		~error ;
		
		// add additional info useful for debugging specific errors...
		
		theErrorCode = 3 ;
		JSONSetElement ( ~error ;
			[ "WindowMode" ; Get ( WindowMode ) ; JSONString ] ;
			[ "SystemPlatform" ; Get ( SystemPlatform ) ; JSONString ] ;
			[ "ApplicationVersion" ; Get ( ApplicationVersion ) ; JSONString ] ;
			[ "HostApplicationVersion" ; Get ( HostApplicationVersion ) ; JSONString ]
		) ;
		
		theErrorCode = 9 or Int ( theErrorCode / 100 ) = 2 ;
		JSONSetElement ( ~error ;
			[ "AccountPrivilegeSetName" ; Get ( AccountPrivilegeSetName ) ; JSONString ] ;
			[ "CurrentPrivilegeSetName" ; Get ( CurrentPrivilegeSetName ) ; JSONString ]
		) ;
		
		/* else */
		~error
	)
)