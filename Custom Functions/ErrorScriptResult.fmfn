/**
 * =====================================
 * ErrorScriptResult ( performScriptName )
 *
 * PURPOSE:
 *	Check for errors after performing a script that returns a JSON result
 *	and an error in the format of the Error Custom Function.
 *
 * RETURNS:
 *	JSON object containing information about an error.
 *
 * PARAMETERS:
 *	performScriptName: name of the script just performed
 *
 * DEPENDENCIES:
 *	Custom Functions:
 *		ErrorFmpGetLast
 *		ErrorApp
 *		ErrorAddScriptStack
 *		ErrorFound
 *
 * HISTORY:
 *	Modified on 2020-MAR-06 by Daniel Smith dan@filemaker.consulting
 *		- improve error message if performScriptName is empty
 *	Created on 2019-MAY-30 by Daniel Smith dan@filemaker.consulting
 *
 * REFERENCES:
 *		https://github.com/dansmith65/FileMaker-Error-Handling
 * =====================================
 */

If ( Get ( LastError ) ;
	ErrorFmpGetLast ( "after perform script" & If ( IsEmpty ( performScriptName ) ; " step" ; ": " & performScriptName ) ) ;
	
	Let ( ~error = JSONGetElement ( Get ( ScriptResult ) ; "error" ) ;
		Case (
			Left ( ~error ; 1 ) = "?" ;
			ErrorApp ( 19 ; performScriptName ) ;
			
			ErrorFound ( ~error ) ;
			ErrorAddScriptStack ( ~error ; "" ) ;
			
			/* Else no error */
			"{\"errorCode\":0}"
		)
	)
)