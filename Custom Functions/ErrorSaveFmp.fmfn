/**
 * =====================================
 * ErrorSaveFmp (  )
 *
 * PURPOSE:
 *		Save the value of Get ( LastError ) to a Let format dictionary stored
 *		in a local variable so it can be retrieved later. If the error is not
 *		0, also store information about the current environment in the local
 *		variable.
 *
 * RETURNS:
 *		(num) value of Get ( LastError )
 *		
 * PARAMETERS: none
 *
 * DEPENDENCIES:
 *		Custom Functions: #
 *
 * NOTES:
 *		local variable name used: $error
 *
 * HISTORY:
 *		CREATED on 2012-NOV-20 Daniel Smith dansmith65@gmail.com
 *		INSPIRED by ErrorString and ErrorData by matt@filemakermagazine.com
 *			https://github.com/filemakerstandards/fmpstandards/tree/master/Functions
 *
 * REFERENCES:
 *		http://www.filemaker.com/help/html/error_codes.html
 * =====================================
 */

Let ( [
	theErrorCode = Get ( LastError ) ;
	$error =
		# ( "errorType" ; "fmp" )
		& # ( "errorCode" ; theErrorCode )

		// Get human-readable error string associated with the error
		// Also add additional information about the environment that is uniqe to the
		//   error code
		& Case (
			theErrorCode < 100 ;
			Case (
				theErrorCode = -1; # ( "errorDescription" ; "Unknown error" );
				theErrorCode = 0; # ( "errorDescription" ; "No error" );
				theErrorCode = 1; # ( "errorDescription" ; "User canceled action" );
				theErrorCode = 2; # ( "errorDescription" ; "Memory error" );
				theErrorCode = 3; # ( "errorDescription" ; "Command is unavailable (for example, wrong operating system, wrong mode, etc.)" )
					& # ( "WindowMode" ; Get ( WindowMode ) )
				;
				theErrorCode = 4; # ( "errorDescription" ; "Command is unknown" );
				theErrorCode = 5; # ( "errorDescription" ; "Command is invalid (for example, a Set Field script step does not have a calculation specified)" );
				theErrorCode = 6; # ( "errorDescription" ; "File is read-only" );
				theErrorCode = 7; # ( "errorDescription" ; "Running out of memory" );
				theErrorCode = 8; # ( "errorDescription" ; "Empty result" );
				theErrorCode = 9; # ( "errorDescription" ; "Insufficient privileges" )
					& # ( "AccountPrivilegeSetName" ; Get ( AccountPrivilegeSetName ) )
				;
				theErrorCode = 10; # ( "errorDescription" ; "Requested data is missing" );
				theErrorCode = 11; # ( "errorDescription" ; "Name is not valid" );
				theErrorCode = 12; # ( "errorDescription" ; "Name already exists" );
				theErrorCode = 13; # ( "errorDescription" ; "File or object is in use" );
				theErrorCode = 14; # ( "errorDescription" ; "Out of range" );
				theErrorCode = 15; # ( "errorDescription" ; "Can't divide by zero" );
				theErrorCode = 16; # ( "errorDescription" ; "Operation failed, request retry (for example, a user query)" );
				theErrorCode = 17; # ( "errorDescription" ; "Attempt to convert foreign character set to UTF-16 failed" );
				theErrorCode = 18; # ( "errorDescription" ; "Client must provide account information to proceed" );
				theErrorCode = 19; # ( "errorDescription" ; "String contains characters other than A-Z, a-z, 0-9 (ASCII)" );
				theErrorCode = 20; # ( "errorDescription" ; "Command/operation canceled by triggered script" );
				# ( "errorDescription" ; "Unknown error code" )
			);
			theErrorCode < 200 ;
			Case (
				theErrorCode = 100; # ( "errorDescription" ; "File is missing" );
				theErrorCode = 101; # ( "errorDescription" ; "Record is missing" );
				theErrorCode = 102; # ( "errorDescription" ; "Field is missing" );
				theErrorCode = 103; # ( "errorDescription" ; "Relationship is missing" );
				theErrorCode = 104; # ( "errorDescription" ; "Script is missing" );
				theErrorCode = 105; # ( "errorDescription" ; "Layout is missing" );
				theErrorCode = 106; # ( "errorDescription" ; "Table is missing" );
				theErrorCode = 107; # ( "errorDescription" ; "Index is missing" );
				theErrorCode = 108; # ( "errorDescription" ; "Value list is missing" );
				theErrorCode = 109; # ( "errorDescription" ; "Privilege set is missing" );
				theErrorCode = 110; # ( "errorDescription" ; "Related tables are missing" );
				theErrorCode = 111; # ( "errorDescription" ; "Field repetition is invalid" );
				theErrorCode = 112; # ( "errorDescription" ; "Window is missing" );
				theErrorCode = 113; # ( "errorDescription" ; "Function is missing" );
				theErrorCode = 114; # ( "errorDescription" ; "File reference is missing" );
				theErrorCode = 115; # ( "errorDescription" ; "Specified menu set is not present" );
				theErrorCode = 116; # ( "errorDescription" ; "Specified layout object is not present" );
				theErrorCode = 117; # ( "errorDescription" ; "Specified data source is not present" );
				theErrorCode = 130; # ( "errorDescription" ; "Files are damaged or missing and must be reinstalled" );
				theErrorCode = 131; # ( "errorDescription" ; "Language pack files are missing (such as template files)" );
				# ( "errorDescription" ; "Unknown error code" )
			);
			theErrorCode < 300 ;
			Case (
				theErrorCode = 200; # ( "errorDescription" ; "Record access is denied" );
				theErrorCode = 201; # ( "errorDescription" ; "Field cannot be modified" );
				theErrorCode = 202; # ( "errorDescription" ; "Field access is denied" );
				theErrorCode = 203; # ( "errorDescription" ; "No records in file to print, or password doesn't allow print access" );
				theErrorCode = 204; # ( "errorDescription" ; "No access to field(s) in sort order" );
				theErrorCode = 205; # ( "errorDescription" ; "User does not have access privileges to create new records; import will overwrite existing data" );
				theErrorCode = 206; # ( "errorDescription" ; "User does not have password change privileges, or file is not modifiable" );
				theErrorCode = 207; # ( "errorDescription" ; "User does not have sufficient privileges to change database schema, or file is not modifiable" );
				theErrorCode = 208; # ( "errorDescription" ; "Password does not contain enough characters" );
				theErrorCode = 209; # ( "errorDescription" ; "New password must be different from existing one" );
				theErrorCode = 210; # ( "errorDescription" ; "User account is inactive" );
				theErrorCode = 211; # ( "errorDescription" ; "Password has expired" );
				theErrorCode = 212; # ( "errorDescription" ; "Invalid user account and/or password; please try again" );
				theErrorCode = 213; # ( "errorDescription" ; "User account and/or password does not exist" );
				theErrorCode = 214; # ( "errorDescription" ; "Too many login attempts" );
				theErrorCode = 215; # ( "errorDescription" ; "Administrator privileges cannot be duplicated" );
				theErrorCode = 216; # ( "errorDescription" ; "Guest account cannot be duplicated" );
				theErrorCode = 217; # ( "errorDescription" ; "User does not have sufficient privileges to modify administrator account" );
				# ( "errorDescription" ; "Unknown error code" )
			);
			theErrorCode < 500 ;
			Case (
				theErrorCode = 300; # ( "errorDescription" ; "File is locked or in use" );
				theErrorCode = 301; # ( "errorDescription" ; "Record is in use by another user" );
				theErrorCode = 302; # ( "errorDescription" ; "Table is in use by another user" );
				theErrorCode = 303; # ( "errorDescription" ; "Database schema is in use by another user" );
				theErrorCode = 304; # ( "errorDescription" ; "Layout is in use by another user" );
				theErrorCode = 306; # ( "errorDescription" ; "Record modification ID does not match" );
				theErrorCode = 400; # ( "errorDescription" ; "Find criteria are empty" );
				theErrorCode = 401; # ( "errorDescription" ; "No records match the request" );
				theErrorCode = 402; # ( "errorDescription" ; "Selected field is not a match field for a lookup" );
				theErrorCode = 403; # ( "errorDescription" ; "Exceeding maximum record limit for trial version of FileMaker Pro" );
				theErrorCode = 404; # ( "errorDescription" ; "Sort order is invalid" );
				theErrorCode = 405; # ( "errorDescription" ; "Number of records specified exceeds number of records that can be omitted" );
				theErrorCode = 406; # ( "errorDescription" ; "Replace/Reserialize criteria are invalid" );
				theErrorCode = 407; # ( "errorDescription" ; "One or both match fields are missing (invalid relationship)" );
				theErrorCode = 408; # ( "errorDescription" ; "Specified field has inappropriate data type for this operation" );
				theErrorCode = 409; # ( "errorDescription" ; "Import order is invalid" );
				theErrorCode = 410; # ( "errorDescription" ; "Export order is invalid" );
				theErrorCode = 412; # ( "errorDescription" ; "Wrong version of FileMaker Pro used to recover file" );
				theErrorCode = 413; # ( "errorDescription" ; "Specified field has inappropriate field type" );
				theErrorCode = 414; # ( "errorDescription" ; "Layout cannot display the result" );
				theErrorCode = 415; # ( "errorDescription" ; "One or more required related records are not available" );
				theErrorCode = 416; # ( "errorDescription" ; "Primary key required from data source table" );
				theErrorCode = 417; # ( "errorDescription" ; "Database is not supported for ODBC operations" );
				# ( "errorDescription" ; "Unknown error code" )
			);
			theErrorCode < 700 ;
			Case (
				theErrorCode = 500; # ( "errorDescription" ; "Date value does not meet validation entry options" );
				theErrorCode = 501; # ( "errorDescription" ; "Time value does not meet validation entry options" );
				theErrorCode = 502; # ( "errorDescription" ; "Number value does not meet validation entry options" );
				theErrorCode = 503; # ( "errorDescription" ; "Value in field is not within the range specified in validation entry options" );
				theErrorCode = 504; # ( "errorDescription" ; "Value in field is not unique as required in validation entry options" );
				theErrorCode = 505; # ( "errorDescription" ; "Value in field is not an existing value in the database file as required in validation entry options" );
				theErrorCode = 506; # ( "errorDescription" ; "Value in field is not listed on the value list specified in validation entry option" );
				theErrorCode = 507; # ( "errorDescription" ; "Value in field failed calculation test of validation entry option" );
				theErrorCode = 508; # ( "errorDescription" ; "Invalid value entered in Find mode" );
				theErrorCode = 509; # ( "errorDescription" ; "Field requires a valid value" );
				theErrorCode = 510; # ( "errorDescription" ; "Related value is empty or unavailable" );
				theErrorCode = 511; # ( "errorDescription" ; "Value in field exceeds maximum number of allowed characters" );
				theErrorCode = 512; # ( "errorDescription" ; "Record was already modified by another user" );
				theErrorCode = 513; # ( "errorDescription" ; "Record must have a value in some field to be created" );
				theErrorCode = 600; # ( "errorDescription" ; "Print error has occurred" );
				theErrorCode = 601; # ( "errorDescription" ; "Combined header and footer exceed one page" );
				theErrorCode = 602; # ( "errorDescription" ; "Body doesn't fit on a page for current column setup" );
				theErrorCode = 603; # ( "errorDescription" ; "Print connection lost" );
				# ( "errorDescription" ; "Unknown error code" )
			);
			theErrorCode < 800 ;
			Case (
				theErrorCode = 700; # ( "errorDescription" ; "File is of the wrong file type for import" );
				theErrorCode = 706; # ( "errorDescription" ; "EPSF file has no preview image" );
				theErrorCode = 707; # ( "errorDescription" ; "Graphic translator cannot be found" );
				theErrorCode = 708; # ( "errorDescription" ; "Can't import the file or need color monitor support to import file" );
				theErrorCode = 709; # ( "errorDescription" ; "QuickTime movie import failed" );
				theErrorCode = 710; # ( "errorDescription" ; "Unable to update QuickTime reference because the database file is read-only" );
				theErrorCode = 711; # ( "errorDescription" ; "Import translator cannot be found" );
				theErrorCode = 714; # ( "errorDescription" ; "Password privileges do not allow the operation" );
				theErrorCode = 715; # ( "errorDescription" ; "Specified Excel worksheet or named range is missing" );
				theErrorCode = 716; # ( "errorDescription" ; "A SQL query using DELETE, INSERT, or UPDATE is not allowed for ODBC import" );
				theErrorCode = 717; # ( "errorDescription" ; "There is not enough XML/XSL information to proceed with the import or export" );
				theErrorCode = 718; # ( "errorDescription" ; "Error in parsing XML file (from Xerces)" );
				theErrorCode = 719; # ( "errorDescription" ; "Error in transforming XML using XSL (from Xalan)" );
				theErrorCode = 720; # ( "errorDescription" ; "Error when exporting; intended format does not support repeating fields" );
				theErrorCode = 721; # ( "errorDescription" ; "Unknown error occurred in the parser or the transformer" );
				theErrorCode = 722; # ( "errorDescription" ; "Cannot import data into a file that has no fields" );
				theErrorCode = 723; # ( "errorDescription" ; "You do not have permission to add records to or modify records in the target table" );
				theErrorCode = 724; # ( "errorDescription" ; "You do not have permission to add records to the target table" );
				theErrorCode = 725; # ( "errorDescription" ; "You do not have permission to modify records in the target table" );
				theErrorCode = 726; # ( "errorDescription" ; "There are more records in the import file than in the target table; not all records were imported" );
				theErrorCode = 727; # ( "errorDescription" ; "There are more records in the target table than in the import file; not all records were updated" );
				theErrorCode = 729; # ( "errorDescription" ; "Errors occurred during import; records could not be imported" );
				theErrorCode = 730; # ( "errorDescription" ; "Unsupported Excel version (convert file to Excel 7.0 (Excel 95), 97, 2000, XP, or 2007 format and try again)" );
				theErrorCode = 731; # ( "errorDescription" ; "The file you are importing from contains no data" );
				theErrorCode = 732; # ( "errorDescription" ; "This file cannot be inserted because it contains other files" );
				theErrorCode = 733; # ( "errorDescription" ; "A table cannot be imported into itself" );
				theErrorCode = 734; # ( "errorDescription" ; "This file type cannot be displayed as a picture" );
				theErrorCode = 735; # ( "errorDescription" ; "This file type cannot be displayed as a picture; it will be inserted and displayed as a file" );
				theErrorCode = 736; # ( "errorDescription" ; "Too much data to export to this format; it will be truncated" );
				theErrorCode = 737; # ( "errorDescription" ; "Bento collection or library is missing; data cannot be imported" );
				# ( "errorDescription" ; "Unknown error code" )
			);
			theErrorCode < 900 ;
			Case (
				theErrorCode = 800; # ( "errorDescription" ; "Unable to create file on disk" );
				theErrorCode = 801; # ( "errorDescription" ; "Unable to create temporary file on System disk" );
				theErrorCode = 802; # ( "errorDescription" ; "Unable to open file" );
				theErrorCode = 803; # ( "errorDescription" ; "File is single user or host cannot be found" );
				theErrorCode = 804; # ( "errorDescription" ; "File cannot be opened as read-only in its current state" );
				theErrorCode = 805; # ( "errorDescription" ; "File is damaged; use Recover command" );
				theErrorCode = 806; # ( "errorDescription" ; "File cannot be opened with this version of FileMaker Pro" );
				theErrorCode = 807; # ( "errorDescription" ; "File is not a FileMaker Pro file or is severely damaged" );
				theErrorCode = 808; # ( "errorDescription" ; "Cannot open file because access privileges are damaged" );
				theErrorCode = 809; # ( "errorDescription" ; "Disk/volume is full" );
				theErrorCode = 810; # ( "errorDescription" ; "Disk/volume is locked" );
				theErrorCode = 811; # ( "errorDescription" ; "Temporary file cannot be opened as FileMaker Pro file" );
				theErrorCode = 813; # ( "errorDescription" ; "Record Synchronization error on network" );
				theErrorCode = 814; # ( "errorDescription" ; "File(s) cannot be opened because maximum number is open" );
				theErrorCode = 815; # ( "errorDescription" ; "Couldn't open lookup file" );
				theErrorCode = 816; # ( "errorDescription" ; "Unable to convert file" );
				theErrorCode = 817; # ( "errorDescription" ; "Unable to open file because it does not belong to this solution" );
				theErrorCode = 819; # ( "errorDescription" ; "Cannot save a local copy of a remote file" );
				theErrorCode = 820; # ( "errorDescription" ; "File is in the process of being closed" );
				theErrorCode = 821; # ( "errorDescription" ; "Host forced a disconnect" );
				theErrorCode = 822; # ( "errorDescription" ; "FMI files not found; reinstall missing files" );
				theErrorCode = 823; # ( "errorDescription" ; "Cannot set file to single-user, guests are connected" );
				theErrorCode = 824; # ( "errorDescription" ; "File is damaged or not a FileMaker file" );
				# ( "errorDescription" ; "Unknown error code" )
			);
			theErrorCode < 1200 ;
			Case (
				theErrorCode = 900; # ( "errorDescription" ; "General spelling engine error" );
				theErrorCode = 901; # ( "errorDescription" ; "Main spelling dictionary not installed" );
				theErrorCode = 902; # ( "errorDescription" ; "Could not launch the Help system" );
				theErrorCode = 903; # ( "errorDescription" ; "Command cannot be used in a shared file" );
				theErrorCode = 905; # ( "errorDescription" ; "No active field selected; command can only be used if there is an active field" );
				theErrorCode = 906; # ( "errorDescription" ; "Current file must be shared in order to use this command" );
				theErrorCode = 920; # ( "errorDescription" ; "Can’t initialize the spelling engine" );
				theErrorCode = 921; # ( "errorDescription" ; "User dictionary cannot be loaded for editing" );
				theErrorCode = 922; # ( "errorDescription" ; "User dictionary cannot be found" );
				theErrorCode = 923; # ( "errorDescription" ; "User dictionary is read-only" );
				theErrorCode = 951; # ( "errorDescription" ; "An unexpected error occurred (*)" );
				theErrorCode = 954; # ( "errorDescription" ; "Unsupported XML grammar (*)" );
				theErrorCode = 955; # ( "errorDescription" ; "No database name (*)" );
				theErrorCode = 956; # ( "errorDescription" ; "Maximum number of database sessions exceeded (*)" );
				theErrorCode = 957; # ( "errorDescription" ; "Conflicting commands (*)" );
				theErrorCode = 958; # ( "errorDescription" ; "Parameter missing (*)" );
				# ( "errorDescription" ; "Unknown error code" )
			);
			theErrorCode < 1300 ;
			Case (
				theErrorCode = 1200; # ( "errorDescription" ; "Generic calculation error" );
				theErrorCode = 1201; # ( "errorDescription" ; "Too few parameters in the function" );
				theErrorCode = 1202; # ( "errorDescription" ; "Too many parameters in the function" );
				theErrorCode = 1203; # ( "errorDescription" ; "Unexpected end of calculation" );
				theErrorCode = 1204; # ( "errorDescription" ; "Number, text constant, field name or \"(\" expected" );
				theErrorCode = 1205; # ( "errorDescription" ; "Comment is not terminated with \"*/\"" );
				theErrorCode = 1206; # ( "errorDescription" ; "Text constant must end with a quotation mark" );
				theErrorCode = 1207; # ( "errorDescription" ; "Unbalanced parenthesis" );
				theErrorCode = 1208; # ( "errorDescription" ; "Operator missing, function not found or \"(\" not expected" );
				theErrorCode = 1209; # ( "errorDescription" ; "Name (such as field name or layout name) is missing" );
				theErrorCode = 1210; # ( "errorDescription" ; "Plug-in function has already been registered" );
				theErrorCode = 1211; # ( "errorDescription" ; "List usage is not allowed in this function" );
				theErrorCode = 1212; # ( "errorDescription" ; "An operator (for example, +, -, *) is expected here" );
				theErrorCode = 1213; # ( "errorDescription" ; "This variable has already been defined in the Let function" );
				theErrorCode = 1214; # ( "errorDescription" ; "AVERAGE, COUNT, EXTEND, GETREPETITION, MAX, MIN, NPV, STDEV, SUM and GETSUMMARY: expression found where a field alone is needed" );
				theErrorCode = 1215; # ( "errorDescription" ; "This parameter is an invalid Get function parameter" );
				theErrorCode = 1216; # ( "errorDescription" ; "Only Summary fields allowed as first argument in GETSUMMARY" );
				theErrorCode = 1217; # ( "errorDescription" ; "Break field is invalid" );
				theErrorCode = 1218; # ( "errorDescription" ; "Cannot evaluate the number" );
				theErrorCode = 1219; # ( "errorDescription" ; "A field cannot be used in its own formula" );
				theErrorCode = 1220; # ( "errorDescription" ; "Field type must be normal or calculated" );
				theErrorCode = 1221; # ( "errorDescription" ; "Data type must be number, date, time, or timestamp" );
				theErrorCode = 1222; # ( "errorDescription" ; "Calculation cannot be stored" );
				theErrorCode = 1223; # ( "errorDescription" ; "The function is not implemented" );
				theErrorCode = 1224; # ( "errorDescription" ; "The function is not defined" );
				theErrorCode = 1225; # ( "errorDescription" ; "The function is not supported in this context" );
				# ( "errorDescription" ; "Unknown error code" )
			);
		// else
			Case (
				theErrorCode = 1300; # ( "errorDescription" ; "The specified name can’t be used" );
				theErrorCode = 1400; # ( "errorDescription" ; "ODBC driver initialization failed; make sure the ODBC drivers are properly installed" );
				theErrorCode = 1401; # ( "errorDescription" ; "Failed to allocate environment (ODBC)" );
				theErrorCode = 1402; # ( "errorDescription" ; "Failed to free environment (ODBC)" );
				theErrorCode = 1403; # ( "errorDescription" ; "Failed to disconnect (ODBC)" );
				theErrorCode = 1404; # ( "errorDescription" ; "Failed to allocate connection (ODBC)" );
				theErrorCode = 1405; # ( "errorDescription" ; "Failed to free connection (ODBC)" );
				theErrorCode = 1406; # ( "errorDescription" ; "Failed check for SQL API (ODBC)" );
				theErrorCode = 1407; # ( "errorDescription" ; "Failed to allocate statement (ODBC)" );
				theErrorCode = 1408; # ( "errorDescription" ; "Extended error (ODBC)" );
				theErrorCode = 1409; # ( "errorDescription" ; "Error (ODBC)" );
				theErrorCode = 1413; # ( "errorDescription" ; "Failed communication link (ODBC)" );
				theErrorCode = 1450; # ( "errorDescription" ; "Action requires PHP privilege extension (*)" );
				theErrorCode = 1451; # ( "errorDescription" ; "Action requires that current file be remote" );
				theErrorCode = 1501; # ( "errorDescription" ; "SMTP authentication failed" );
				theErrorCode = 1502; # ( "errorDescription" ; "Connection refused by SMTP server" );
				theErrorCode = 1503; # ( "errorDescription" ; "Error with SSL" );
				theErrorCode = 1504; # ( "errorDescription" ; "SMTP server requires the connection to be encrypted" );
				theErrorCode = 1505; # ( "errorDescription" ; "Specified authentication is not supported by SMTP server" );
				theErrorCode = 1506; # ( "errorDescription" ; "Email(s) could not be sent successfully" );
				theErrorCode = 1507; # ( "errorDescription" ; "Unable to log in to the SMTP server" );
				# ( "errorDescription" ; "Unknown error code" )
			)
		)

		// add additional info about the current environment to the ErrorObject
		// these values should be adjusted as needed for the solution
		& If (
			theErrorCode <> 0 ;
			""
				& # ( "AllowAbortState" ; Get ( AllowAbortState ) )
				& # ( "CurrentHostTimestamp" ; Get ( CurrentHostTimestamp ) )
				& # ( "ErrorCaptureState" ; Get ( ErrorCaptureState ) )
				& # ( "LastODBCError" ; Get ( LastODBCError ) )
				& # ( "ScriptName" ; Get ( ScriptName ) )
				& # ( "ScriptParameter" ; Get ( ScriptParameter ) )
				& # ( "ScriptResult" ; Get ( ScriptResult ) )
				& # ( "UserCount" ; Get ( UserCount ) )
		)
] ;
	theErrorCode
) // end of Let